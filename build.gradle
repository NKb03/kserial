buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'maven-publish'
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.bmuschko.nexus'

archivesBaseName = 'kserial'
group 'com.github.nkb03'
version '0.2-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

static String kotlin(Object dep) {
    return "org.jetbrains.kotlin:kotlin-$dep:1.3.70"
}

static String spek(Object dep) {
    return "org.jetbrains.spek:spek-$dep:1.1.5"
}

dokka {
    outputFormat = "javadoc"
    outputDirectory = "$projectDir/javadoc"
    includes = ["sfts.md"]
    samples = ["src/samples/"]
    includeNonPublic = false
    reportUndocumented = true
    impliedPlatforms = ["JVM"]
    noStdlibLink = false
}

task sourcesJar_(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar_(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar_
            artifact javadocJar_
        }
    }
}

modifyPom {
    project {
        name 'kserial'
        description 'Yet another serialization library for the JVM'
        url 'https://github.com/nkb03/kserial'
        inceptionYear '2020'

        scm {
            url 'https://github.com/nkb03/kserial'
            connection 'git:https://github.com/nkb03/kserial.git'
            developerConnection 'git:git://github.com/nkb03/kserial.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'santaclaus'
                name 'Nikolaus Knop'
                email 'niko.knop003@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

dependencies {
    implementation kotlin("stdlib-jdk8")
    implementation 'com.github.nkb03:bundles:0.1-SNAPSHOT'
    testImplementation(spek("api")) {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation(spek("junit-platform-engine")) {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "com.natpryce:hamkrest:1.5.0.0"
    implementation kotlin("reflect")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}